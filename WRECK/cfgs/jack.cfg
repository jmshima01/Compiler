Class -> class ClassName { ClassVarDec SubroutineDec } $
ClassVarDec -> ClassVarDecSF Type VarName ExtraVarExt ; ClassVarDec
| lambda
ClassVarDecSF -> static | field
ExtraVarExt -> , VarName ExtraVarExt
| lambda
Type -> int | char | boolean | ClassName
SubroutineDec -> SubroutineDecCFM SubroutineDecType SubroutineName ( ParameterList ) SubroutineBody SubroutineDec
| lambda
SubroutineDecCFM -> constructor | function | method
SubroutineDecType -> void | Type
ParameterList -> Type VarName ParameterListExt
| lambda
ParameterListExt -> , Type VarName ParameterListExt
| lambda
SubroutineBody -> { SubroutineBodyVarDec Statements }
SubroutineBodyVarDec -> VarDec SubroutineBodyVarDec
| lambda
VarDec -> var Type VarName VarDecExt ;
VarDecExt -> , VarName VarDecExt
| lambda
ClassName -> identifier
SubroutineName -> identifier
VarName -> identifier
Statements -> Statement Statements
| lambda
Statement -> LetStatement | IfStatement | WhileStatement | DoStatement | ReturnStatement
LetStatement -> let VarName LetExpressionCheck = Expression ;
LetExpressionCheck -> [ Expression ]
| lambda
IfStatement -> if ( Expression ) { Statements } IfElseCheck
IfElseCheck -> else { Statements }
| lambda
WhileStatement -> while ( Expression ) { Statements }
DoStatement -> do SubroutineCall ;
ReturnStatement -> return ReturnExpressionCheck ;
ReturnExpressionCheck -> Expression
| lambda
Expression -> Term ExpressionTerms
ExpressionTerms -> Op Term ExpressionTerms
| lambda
Term -> integerconstant | stringconstant | KeywordConstant | VarName VarNameTerm
| SubroutineCall | ( Expression ) | UnaryOp Term
VarNameTerm -> [ Expression ]
| lambda 
SubroutineCall -> SubroutineName ( ExpressionList ) | SubroutineCallNames . SubroutineName ( ExpressionList )
SubroutineCallNames -> objectname
ExpressionList -> Expression ExpressionListExt
| lambda
ExpressionListExt -> , Expression ExpressionListExt
| lambda
Op -> + | - | * | / | & | pipe | < | > | =
UnaryOp -> - | ~
KeywordConstant -> true | false | null | this